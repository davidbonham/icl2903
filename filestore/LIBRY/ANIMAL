BU
100 REM PAUL REDHEAD/JOHN HAMMOND  15/4/76  ANIMAL RECOGNITION
110 DIM Q$[200],Q[99,1]
120 PRINT
130 PRINT "THIS PROGRAM HAS A LIST OF ANIMALS WHICH IT CAN IDENTIFY,"
140 PRINT "AND IF YOU WILL THINK OF ONE IT WILL ATTEMPT TO GUESS IT"
150 PRINT "BY ASKING YOU QUESTIONS.  PLEASE ANSWER THESE QUESTIONS"
160 PRINT "TRUTHFULLY BY TYPING YES OR NO. IF THE PROGRAM IS UNABLE"
170 PRINT "TO FIND YOUR ANIMAL, IT WILL ASK YOU WHAT IT IS AND ADD"
180 PRINT "IT TO ITS LIST"
190 Q$[0]="IS IT A MAMMAL"
200 Q$[1]="DOES IT LIVE IN THE SEA"
210 Q$[2]="IS IT A FISH"
220 Q$[3]="IS IT A BIRD"
230 Q$[4]="IS IT FURRY"
240 Q$[100]="A CROCODILE"
250 Q$[101]="YOU"
260 Q$[102]="A WHALE"
270 Q$[103]="A KIPPER"
280 Q$[104]="AN OSTRICH"
290 Q$[105]="A FERRET"
300 Q[0,0]=1!Q[0,1]=2
310 Q[1,0]=102!Q[1,1]=4
320 Q[2,0]=103!Q[2,1]=3
330 Q[3,0]=104!Q[3,1]=100
340 Q[4,0]=105!Q[4,1]=101
350 N1=5
360 GOTO 390
370 PRINT "PLEASE FIND SOMEONE ELSE TO CARRY ON"
380 PRINT
381 PRINT
390 PRINT
391 PRINT "PLEASE THINK OF AN ANIMAL"
400 GOTO 420
410 PRINT "PLEASE THINK OF ANOTHER ANIMAL"
420 P=0
430 PRINT Q$[P];
440 A=0
450 P1=P
460 INPUT A$
470 IF A$="YES" THEN 520
480 IF A$="NO" THEN 510
490 PRINT "YES OR NO";
500 GOTO 640
510 A=1
520 P=Q[P,A]
530 IF P < 100 THEN 430
540 PRINT "IS IT ";Q$[P];
550 INPUT A$
560 IF A$="NO" THEN 660
570 IF A$="YES" THEN 600
580 PRINT "YES OR NO";
590 GOTO 550
600 PRINT "I WIN. HAVE YOU HAD ENOUGH YET";
610 INPUT A$
620 IF A$="YES" THEN 370
630 IF A$="NO" THEN 410
640 PRINT "YES OR NO";
650 GOTO 610
660 PRINT "WHAT IS IT THEN";
670 INPUT X$
680 PRINT "IF I WANTED TO TELL THE DIFFERENCE BETWEEN ";
690 PRINT X$;" AND ";Q$[P];" WHAT QUESTION WITH A YES/NO ANSWER ";
700 PRINT "SHOULD I ASK"
710 INPUT Y$
720 PRINT "AND THE ANSWER FOR ";X$;" WOULD BE";
730 INPUT A$
740 IF A$="YES" THEN 790
750 IF A$="NO" THEN 780
760 PRINT "YES OR NO";
770 GOTO 730
780 T$=X$!X$=Q$[P]!Q$[P]=T$!REM SWOP OBJECTS
790 N2=N1+101
800 Q$[N1]=Y$
810 Q$[N2]=X$
820 Q[N1,1]=P
830 Q[N1,0]=N2
840 Q[P1,A]=N1
850 N1=N1+1
860 GOTO 410
870 END
