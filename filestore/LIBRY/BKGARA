BU
1 REM : GARAGE QUEUEING SIMULATIOM
2 REM : 2 PUMPS
3 REM : 1 CAR ARRIVES EVERY 2 MINUTES (AVERAGE)
4 REM : SERVICE TIME IS 3 MINUTES
10 DIM Q(10),M$(2)
20 LET M$(1) = "CAR ARRIVED"
30 LET M$(2) = "**NO CAR**"
40 LET T = 0
50 LET A = 0
60 LET B = 0
70 LET C = 0
80 LET N = 0
90 PRINT "TIME";TAB(8);"EVENT";TAB(20);"PUMP A";TAB(27);"PUMP B";TAB(34);"QUEUE"
100 LET T=T+1
110 IF T < A THEN 300
120 LET A1=0
300 IF T < B THEN 500
310 LET B1 = 0
500 REM : MOVE CARS FROM QUEUE IF POSSIBLE
510 IF C = 0 THEN 700
520 IF A1 > 0 THEN 600
525 REM : MOVE CAR TO PUMP A
530 LET A1=Q(1)
540 LET A = T+ 3
560 LET C=C-1
570 FOR K=1 TO C
580 LET Q(K)=Q(K+1)
590 NEXT K
600 IF B1 > 0 THEN 700
610 REM : MOVE CAR TO PUMP B
620 LET B1=Q(1)
630 LET B = T+3
640 LET C = C - 1
650 IF C=0 THEN 700
660 FOR K = 1 TO C
670 LET A(K)=Q(K+1)
680 NEXT K
700 LET R = INT(2*RND)+1
710 IF R = 2 THEN 900
720 REM : MEW CAR ARRIVES
730 LET N=N+1
740 REM : TEST FOR A FREE PUMP
750 IF A1=0 THEN 2000
760 IF B1=0 THEN 2100
770 IF C=10 THEN 2200
780 REM : A CAR JOINS THE QUEUE
790 LET C = C+1
800 LET Q(C) = N
900 REM : PRINT DETAILS FOR THIS MINUTE
910 PRINT T;TAB(8);M$(R);TAB(22);
920 IF A1=0 THEN 2300
930 PRINT A1;TAB(28);
1000 IF B1=0 THEN 2400
1010 PRINT B1;TAB(34);
1100 IF C=0 THEN 1200
1110 FOR K=1 TO C
1120 PRINT Q(K);
1130 NEXT K
1200 PRINT
1210 IF T < 540 THEN 100
1220 STOP
2000 REM : MOVE CAR TO PUMP A
2010 LET A1=N
2020 LET A=T+3
2030 GOTO 900
2100 REM: MOVE CAR TO PUMP B
2110 LET B1=N
2120 LET B=T+3
2130 GOTO 900
2200 REM : CAR ARRIVED - QUEUE FULL
2210 PRINT "QUEUE FULL. CAR NUMBER";N;"HAS MOVED ON."
2220 GOTO 900
2300 REM : PUMP A IS FREE - PRINT SPACE
2310 PRINT TAB(28);
2320 GOTO 1000
2400 REM : PUMP B IS FREE - PRINT SPACE
2410 PRINT TAB(34);
2420 GOTO 1100
9999 END
