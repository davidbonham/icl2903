BU
100 REM JOHN HAMMOND  9/3/76
105 PRINT
110 PRINT "GARAGE SIMULATION"
111 PRINT
120 PRINT "DO YOU WANT INSTRUCTIONS";
130 INPUT A$
140 IF A$ = "NO" THEN 240
150 IF A$ = "YES" THEN 185
160 PRINT "PLEASE ANSWER YES OR NO"
170 GOTO 130
185 PRINT
190 PRINT "THIS PROGRAM SIMULATES THE OPERATION OF A PETROL SERVICE"
200 PRINT "STATION; YOU ENTER THE NUMBER OF PUMPS, THE MAXIMUM QUEUE"
210 PRINT "SIZE BEFORE NEW ARRIVALS ARE TURNED AWAY, THE LENGTH OF"
220 PRINT "TIME IT TAKES TO SERVE A CAR AT A PUMP, HOW OFTEN THE CARS"
230 PRINT "ARRIVE, AND HOW LONG THE SIMULATION LASTS."
235 PRINT
240 PRINT
250 DIM Q[12],P[8],N[8]
266 RANDOMIZE
270 REM INPUT VARIABLES
280 PRINT "NUMBER OF PUMPS";
290 INPUT P
295 IF P <> INT(P) THEN 310
300 IF P >= 1 THEN 330
310 PRINT "TRY AGAIN";
320 GOTO 290
330 IF P <= 8 THEN 360
340 PRINT "TOO MANY - MAXIMUM IS 8; ";
350 GOTO 290
360 PRINT "MAXIMUM QUEUE SIZE";
370 INPUT L
375 IF L <> INT(L) THEN 390
380 IF L >= 0 THEN 410
390 PRINT "TRY AGAIN";
400 GOTO 370
410 IF L <= 12+INT(-1.5*P) THEN 440
420 PRINT "TOO MANY - MAXIMUM IS";12+INT(-1.5*P);"- ";
430 GOTO 390
440 PRINT "SERVICE TIME (MINUTES)";
450 INPUT S
460 IF S>0 THEN 490
470 PRINT "TRY AGAIN";
480 GOTO 450
490 PRINT "MEAN INTERVAL BETWEEN ARRIVALS (MINUTES)";
500 INPUT I
510 IF I>0 THEN 540
520 PRINT "TRY AGAIN";
530 GOTO 500
540 LET I2=I+I
550 PRINT "LENGTH OF SIMULATION (MINUTES)";
560 INPUT F
562 IF F < 1 THEN 566
564 IF F=INT(F) THEN 570
566 PRINT "TRY AGAIN"
568 GOTO 560
570 PRINT
580 PRINT
590 PRINT
599 REM PRINT HEADINGS
600 PRINT "TIME"; TAB(24);
610 FOR K=1 TO P
620 PRINT "PUMP  ";
630 NEXT K
635 IF L=0 THEN 645
640 PRINT " QUEUE";
645 PRINT
650 FOR K=1 TO P
660 PRINT TAB(17+6*K);K;
670 NEXT K
680 PRINT
681 PRINT
682 LET N=T1=C=0
684 FOR K=1 TO P
686 LET P[K]=N[K]=0
688 NEXT K
690 REM SAMPLE EACH MINUTE
700 FOR T=1 TO F
710 LET J=0
711 REM J COUNTS NEW CARS
720 GOTO 750
730 LET J=J+1
740 LET T1=T1+I2*RND
741 REM T1 IS ARRIVAL TIME OF NEW CAR
750 IF T1 <= T THEN 730
760 LET C1=J1=0
779 REM MOVE CARS TO FREE PUMPS
780 FOR K=1 TO P
790 IF P[K]>T THEN 930
799 REM ANY LEFT IN QUEUE?
800 IF C1=C THEN 860
810 LET C1=C1+1
819 REM MOVE CAR FROM QUEUE TO PUMP
820 LET N[K]=Q[C1]
829 REM SET TIME WHEN PUMP WILL BECOME FREE
830 LET P[K]=T+S
840 GOTO 930
859 REM ANY NEW ARRIVALS FOR FREE PUMPS?
860 IF J1=J THEN 920
870 LET J1=J1+1
879 REM MOVE NEW ARRIVAL TO PUMP
880 LET N[K]=N+J1
889 REM SET TIME WHEN PUMP WILL BECOME FREE
890 LET P[K]=T+S
900 GOTO 930
919 REM MARK PUMP AS FREE
920 LET N[K]=0
930 NEXT K
939 REM KEEP OLD QUEUE SIZE
940 LET C2=C
959 REM SET NEW QUEUE SIZE
960 LET C=C-C1+J-J1
969 REM SET CAR NUMBER OF LATEST ARRIVAL
970 LET N=N+J
975 LET F=0
979 REM QUEUE TOO LARGE?
980 IF C <= L THEN 1088
995 LET F=C-L
997 PRINT "PUMP";
999 IF P=1 THEN 1003
1001 PRINT "S";
1003 IF L=0 THEN 1007
1005 PRINT " AND QUEUE";
1007 PRINT " FULL, CAR";
1010 IF F=1 THEN 1030
1020 PRINT "S";
1029 REM LIST CARS TURNED AWAY
1030 FOR K=N-F+1 TO N
1040 PRINT K;
1050 NEXT K
1060 PRINT "TURNED AWAY"
1070 LET C=L
1088 IF C=0 THEN 1140
1089 REM FORM A QUEUE
1090 FOR K=1 TO C
1092 IF C1=C2 THEN 1100
1094 LET C1=C1+1
1096 LET Q[K]=Q[C1]
1098 GOTO 1110
1100 LET Q[K]=N-C+K-F
1110 NEXT K
1140 PRINT T;TAB(6);"*";J;
1150 IF J=1 THEN 1180
1160 PRINT "CARS ARRIVE";
1170 GOTO 1190
1180 PRINT "CAR ARRIVES";
1190 PRINT TAB(22);"*";
1200 FOR K=1 TO P
1205 IF N[K]=0 THEN 1215
1210 PRINT N[K];
1215 PRINT TAB(22+6*K);"*";
1220 NEXT K
1230 PRINT "*";
1235 IF C=0 THEN 1270
1239 REM PRINT CARS IN QUEUE
1240 FOR K=1 TO C
1250 PRINT Q[K];
1260 NEXT K
1270 PRINT
1280 NEXT T
1300 PRINT
1310 PRINT
1320 PRINT "END OF SIMULATION"
1370 END