BU
100 DIM I$[3],P[2],E[2],V[2],S[2],X[2],M[2],C[2],B[2],Y[2]
110 DIM Q[2]
120 PRINT "WELCOME TO THE NSP 1/4 MILE DRAG STRIP"
130 PRINT "WOULD YOU LIKE INSTRUCTIONS";
140 INPUT I$
150 IF I$ = "NO" THEN 230
160 IF I$ = "YES" THEN 190
170 PRINT "PLEASE ANSWER YES OR NO";
180 GOTO 140
190 PRINT "YOU MAY RACE AGAINST ONE OF YOUR FRIENDS OR "
200 PRINT "AGAINST MY DRAGSTER. YOU WILL BE ASKE TO DESIGN YOUR"
210 PRINT "OWN MACHINE, SPECIFYIG HORSEPOWER, READ END RATIO (X:1),"
220 PRINT "TYRE WIDTH IN INCHES AND TYRE DIAMETER IN FEET."
230 PRINT "DO YOU WANT TO RACE AGAINST ME";
240 INPUT I$
250 IF I$ = "NO" THEN 370
255 IF I$ = "YES" THEN 270
260 PRINT "PLEASE ANSWER YES OR NO";
265 GOTO 240
270 PRINT "I WILL HAVE CAR 1"
280 P[1]=600
290 PRINT "HORSEPOWER ="; P[1]
300 E[1]=5.9
310 PRINT "REAR END RATIO =";E[1]
320 W[1]=22
330 PRINT "TYRE WIDTH =";W[1]
340 D[1]=3.9
350 PRINT "TYRE DIAMETER =";D[1]
360 K0=2!GOTO 380
370 K0=1
380 FOR I=K0 TO 2
390 PRINT "DESIGN CAR";I;":"
400 PRINT "HORSEPOWER = ";
410 INPUT P[I]!IF P[I]>=0 THEN IF P[I] <= 1000 THEN 430
420 PRINT "BETWEEN 0 AND 1000 PLEASE";
421 GOTO 410
430 PRINT "REAR END RATIO = ";
440 INPUT E[I]!IF E[I] >= 0 THEN IF E[I] <= 10 THEN 460
450 PRINT "BETWEEN 1 AND 10 PLEASE";
451 GOTO 440
460 PRINT "TYRE WIDTH = ";
470 INPUT W[I]!IF W[I] >= 1 THEN IF W[I] <= 30 THEN 490
480 PRINT "BETWEEN 1 AND 30 PLEASE";
481 GOTO 470
490 PRINT "TYPE DIAMETER = ";
500 INPUT D[I]!IF D[I] >= 0.5 THEN IF D[I] <= 5 THEN 520
510 PRINT "BETWEEN 0.5 AND 5 PLEASE";
511 GOTO 500
520 NEXT I
530 PRINT
540 PRINT "GO!"
550 K1 = 500
560 K2=1.6
570 K3=2
580 K4=0.0006
590 K7=4
600 K8=0.00015
610 Q[1]=Q[2]=0
620 S[1]=S[2]=0
630 X[1]=X[2]=0
640 REM : M IS MASS
650 J = 1
660 M[J]=(K1+K2*P[J]+K3*W[J]*D[J]+K2*D[J]^2)/32.2
670 REM : C IS DRAG FROM WIND
680 C[J]=K4*M[J]^(2/3)+K8*W[J]*D[J]
690 REM : B IS THE MAX ACCELERATION WITHOUT BURNING
700 B[J]=15+28*W[J]*D[J]/((W[J]+6)*(D[J]+1))
710 REM : Y IS THE SCALE FACTOR FOR RMP VS POWER
720 Y[J]=3.7-0.0033*P[J]
730 J = J+1
740 IF J <= 2 THEN 660
750 PRINT
760 PRINT
770 PRINT "ELAPSED";TAB(15);"CAR 1";TAB(39);"CAR 2"
780 PRINT "TIME   SPEED      DISTANCE     SPEED      DISTANCE"
790 PRINT "(SEC)  (MPH)        (FT)       (MPH)        (FT)"
800 PRINT
810 FOR J=1 TO 2!Z[J]=60*E[J]/(CPI*D[J])!A[J]=C[J]/M[J]
820 T[J]=2*E[J]*P[J]*0.97647/(D[J]*M[J])
830 U[J]=T[J]*(1.87952E-4/Y[J]-0.61446E-4)
840 V[J]=T[J]*0.963856E-11/(Y[J]*Y[J]*Y[J])
850 NEXT J
860 T=0
870 T1=1
880 J=1
890 REM : R IS RPM
900 R=Z[J]*S[J]
910 REM F IS FORCE ON ROAD FROM TIRES
920 F=T[J]+(U[J]+V[J]*R*R)*R
930 REM : TEST FOR BURN
940 IF F>B[J] THEN 1010
950 REM : A=ACCELERATION
960 IF Q[J] <> 0 THEN 990
970 PRINT "CAR";J;"STOPS BURNING RUBBER"
980 Q[J]=1
990 A=F-A[J]*S[J]*S[J]
1000 GOTO 1030
1010 A=B[J]-A[J]*S[J]*S[J]
1020 REM : S=SPEED IN FT/SEC
1030 S[J]=S[J]+A*0.5
1040 REM : X=DISTANCE IN FT
1050 X[J]=X[J]+S[J]*0.5
1060 J=J+1
1070 IF J<=2 THEN 890
1080 REM  : TEST FOR FINISH
1090 IF X[1] >= 1320 THEN 1110
1100 IF X[2] < 1320 THEN 1300
1110 IF X[1]>X[2] THEN 1210
1111 Z3=X[1]-X[2]
1120 T3=(X[2]-1320)/S[2]
1130 T=INT(100*(T+T1/2-T3)) /100
1140 X[2]=1320
1150 X[1]=X[1]-S[1]*T3
1160 PRINT T; TAB(8);S[1]/1.466667;TAB(19);X[1];TAB(32);S[2]/1.466667;
1170 PRINT TAB(43);X[2]
1171 IF Z3 <> 0 THEN 1180!PRINT TAB(26);"DRAW"!PRINT!STOP
1180 PRINT TAB(40);"WINNER"
1190 PRINT
1200 STOP
1210 T3=(X[1]-1320)/S[1]
1220 T=INT(100*(T+T1/2-T3))/100
1230 X[1]=1320
1240 X[2]=X[2]-S[2]*T3
1250 PRINT T;TAB(8);S[1]/1.466667;TAB(19);X[1];TAB(32);S[2]/1.466667;
1260 PRINT TAB(43);X[2]
1270 PRINT TAB(10);"WINNER"
1280 PRINT
1290 STOP
1300 T1=T1+1
1310 IF T1<=2 THEN880
1320 PRINT T+1;TAB(8);S[1]/1.466667;TAB(19);X[1];TAB(32);S[2]/1.466667;
1330 PRINT TAB(43);X[2]
1340 T=T+1
1350 GOTO 870
1360 END