BU
5 LET W=0
10 PRINT "THIS PROGRAM FINDS ON WHICH"
20 PRINT "DAY OF THE WEEK THE DATE YOU"
30 PRINT "TYPE FALLS ON, USING ZELLER'S"
40 PRINT "CONGRUENCE"
45 GOTO 590
50 PRINT "TYPE THE CENTURY"
60 INPUT C
70 LET C2=C-1
80 PRINT "TYPE THE YEAR"
90 INPUT Y
91 IF Y<100 THEN 100
92 PRINT "PLEASE TYPE IN THE YEAR AGAIN (TWO DIGITS ONLY) "
93 GOTO 90
100 LET Y2=Y
110 REM TEST FOR PRE-1752 DATE
120 IF C2>17 THEN 150
130 IF C2<>17 THEN 570
140 IF Y2<52 THEN 570
150 PRINT "TYPE THE MONTH"
160 INPUT M
170 LET M2=M
180 PRINT "TYPE THE DATE"
190 INPUT D
200 LET D2=D
205 GOTO 700
210 REM MAKE JAN & FEB LAST TWO MONTHS OF 
220 REM PREVIOUS YEAR
230 IF M2>2 THEN 290
240 LET M2=M2+12
250 LET Y2=Y2-1
260 IF Y2>0 THEN 290
270 LET Y2=Y2+100
280 LET C2=C2-1
290 LET M2=M2-2
300 REM CALCULATION BEGINS HER
310 LET A=INT(2.6*M2-0.2)+D2+Y2-2*C2
320 LET B=A+INT(Y2/4)+INT(C2/4)
330 REM REDUCE RESULT TO MOD 7
340 IF B<0 THEN 380
350 IF B<7 THEN 400
360 LET B=B-7
370 GOTO 340
380 LET B=B+7
390 GOTO 340
400 REM CONVERT NUMBER TO DAY
410 PRINT D;"/";M;"/";(C-1)*100+Y;" FALLS ON A ";
420 IF B=0 THEN 430
421 IF B=1 THEN 450
422 IF B=2 THEN 470
423 IF B=3 THEN 490
424 IF B=4 THEN 510
425 IF B=5 THEN 530
426 IF B=6 THEN 550
430 PRINT "SUNDAY"
440 GOTO 590
450 PRINT "MONDAY"
460 GOTO 590
470 PRINT "TUESDAY"
480 GOTO 590
490 PRINT "WEDNESDAY"
500 GOTO 590
510 PRINT "THURSDAY"
520 GOTO 590
530 PRINT "FRIDAY"
540 GOTO 590
550 PRINT "SATURDAY"
560 GOTO 590
570 PRINT "ZELLER'S CONGRUENCE DOES"
580 PRINT "NOT APPLY TO A PRE-1752 DATE"
590 PRINT "DO YOU WISH TO TYPE A DATE ?"
600 INPUT X$
610 IF X$="YES" THEN 50
615 GOSUB 930
620 IF X$="NO" THEN 999
630 PRINT "PLEASE ANSWER YES OR NO."
640 GOTO 600
700 REM TEST FOR MEANINGLESS DATE
710 IF M<=12 THEN 740
720 PRINT "THERE ARE ONLY TWELVE MONTHS IN A YEAR"
730 GOTO 150
740 IF D<32 THEN 770
750 PRINT "THERE ARE NOT ";D;" DAYS IN THIS MONTH"
755 GOSUB 930
760 GOTO 180
770 IF M=2 THEN 850
780 IF M=4 THEN 830
790 IF M=6 THEN 830
800 IF M=9 THEN 830
810 IF M=11 THEN 830
820 GOTO 210
830 IF D<31 THEN 210
840 GOTO 750
850 LET T=(C2*100+Y)/4
860 LET T2=INT(T)
870 IF T=T2 THEN 900
880 IF D>28 THEN 750
890 GOTO 210
900 IF D>29 THEN 750
910 GOTO 210
925 REM WARNING SUBROUTINE
930 LET W=W+1
940 IF W=3 THEN 970
950 IF W=4 THEN 986
960 RETURN
970 PRINT "YOU HAVE MADE THREE ERRORS, BUT HAVE ONE MORE CHANCE."
980 PRINT "LESSON:"
981 PRINT "MONTH: JAN FEB MAR APR MAY JUN JLY AUG SEPT OCT NOV DEC"
982 PRINT "DAYS:   31  28  31  30  31  30  31  31   30  31  30  31"
983 PRINT "IN A LEAP YEAR, FEB HAS 29 DAYS. THERE ARE 12 MONTHS"
984 PRINT "IN A YEAR AND 100 YEARS IN A CENTURY."
985 GOTO 960
986 PRINT "YOU HAVE BEEN WARNED. YOU ARE WASTING TERMINAL TIME"
987 PRINT "THIS PROGAM WILL BE TERMINATED. TYPE 'RUN' WHEN YOU ARE"
988 PRINT "READY TO USE IT PROPERLY!!!!!!!!"
989 GOTO 999
999 END
