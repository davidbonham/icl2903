BU//0
10 DIM X[100],Y[100]
20 REM CALCULATION OF CORRELATION COEFFICIENT -- D.BONHAM -- 21/2/77
30 S1=S2=A3=A4=A5=0
40 PRINT 
50 PRINT "CALCULATION OF CORRELATION COEFFICIENT."
60 PRINT 
70 REM INPUT NO. OF PAIRS :
80 PRINT "HOW MANY DATA PAIRS ";
90 INPUT N
100 PRINT 
110 PRINT "ENTER DATA PAIRS X,Y NOW:"
120 PRINT 
130 FOR I=1 TO N
140 INPUT X[I],Y[I]
150 REM UPDATE SUM:
160 S1=S1+X[I]
170 S2=S2+Y[I]
180 NEXT I
190 PRINT 
200 M1=S1/N
210 M2=S2/N
220 PRINT "MEAN OF X-VALUES =";M1
225 PRINT 
230 PRINT "MEAN OF Y-VALUES =";M2
240 PRINT 
250 PRINT MUL$("=",72)
260 PRINT "X - MEAN","Y - MEAN","(X-MEAN)^2","(Y-MEAN)^2";
270 PRINT TAB(58);"X-MEAN*Y-MEAN"
280 PRINT MUL$("=",72)
290 FOR I=1 TO N
300 D1=X[I]-M1!REM X - MEAN
310 D2=Y[I]-M2!REM Y - MEAN
320 A3=A3+D1^2!REM SUM SQUARE
330 A4=A4+D2^2!REM SUM SQUARE
340 A5=A5+D2*D1!REM SUM X*Y
350 PRINT D1,D2,D1^2,D2^2,D1*D2
360 NEXT I
370 PRINT MUL$("=",72)
380 PRINT "TOTALS:";
390 PRINT ,,A3,A4,A5
400 PRINT MUL$("=",72)
410 PRINT 
411 C$=""
412 C=M2-A5/A3*M1
413 IF C#0 LET C$=" "&SGN$(C)&" "&STR$(ABS(C))
414 M$=STR$(A5/A3)
415 PRINT "LINE OF REGRESSION OF Y ON X : Y = ";M$;"X";C$
416 PRINT 
420 PRINT "COVARIANCE = ";A5/(N-1)
430 PRINT 
440 PRINT "CORRELATION COEFFICIENT = ";
450 PRINT A5/SQR(A3*A4)
460 PRINT 
470 END
