BU
860 REM ***************INTERPRETER SECTION******************************  
870 DATA 20,40 
880 READ S1,S2!REM <STORE SIZE>,<PROGRAM SIZE>  
890 PRINT "NENE 2903E INTERACTIVE CESIL "
899 T2$=TIM$
900 PRINT "DATE: ";DAT$;" TIME: ";T2$
910 REM FIXMEPRINT "SYSTEM READY"!PRINT 
970 L=N=J=H=I=Z=D4=X=0
980 D$=" "
990 DIM P$[40],Q$[40],R$[40],S$[20],S[20]
1000 GOTO 1400
1005 REM *******************PROGRAM SECTION*****************************
1010 L=1!D$=L$=" "
1020 FOR A=1 TO S1!REM SET UP STORE
1030 LET S[A]=0
1040 S$[A]=" "
1050 NEXT A 
1060 H=0
1070 REM   
1080 FOR A=1 TO S2-1
1090 P$[A]=Q$[A]=R$[A]=""!REM SET UP PROGRAM STORE  
1100 NEXT A 
1110 REM   
1120 FOR N=L TO S2
1130 GOSUB 1150
1140 ON I GOTO 1400,3290,1330
1150 REM INPUT SUBROUTINE 1-%,2-ERR,3-OTHER
1160 LINPUT I1$!I1$=SEG$(I1$,2)
1161 IF OCC(I1$,CHR$(18))=0 THEN 1165
1162 I$=SEG$(I1$,1,POS(I1$,CHR$(18))-1)
1163 I2$=SEG$(I1$,POS(I1$,CHR$(18)))
1164 GOTO 1166
1165 I$=I1$!I2$=""
1166 IF OCC(I$,"  ")=0 THEN 1169
1167 I$=SRP$(I$,"  "," ")
1168 GOTO 1166
1169 I$=I$&I2$
1175 P$[N]=Q$[N]=R$[N]=" "
1180 IF I$ = "%" THEN 1310
1190 I1=POS(I$," ")
1200 I2$=SEG$(I$,1,3)
1210 IF I2$ = "LOA" OR I2$ = "LIN" OR SUB$(I$,1)#"L" THEN 1240
1220 P$[N]=SEG$(I$,1,I1-1)
1230 I$=SEG$(I$,I1+1)
1240 I2=POS(I$," ")
1250 IF I2#0 LET R$[N]=SEG$(I$,I2+1)
1260 Q$[N]=SEG$(I$,1,3)
1270 IF Q$[N] = "ERR" THEN 1300
1280 IF SEG$(Q$[N],1,3) = "HAL" LET H=H+1
1290 I=3!GOTO 1320
1300 I=2!GOTO 1320
1310 P$[N]="%"!I=1
1320 RETURN
1330 L=N+1!REM L IS LENGTH OF PROGRAM USED FOR LISTING ETC  
1340 IF N=S2-10 PRINT "***TEN INSTRUCTIONS LEFT***"
1350 NEXT N 
1360 REM   
1370 PRINT "***PROGRAM TOO LARGE***"
1380 GOTO 3530
1390 REM *************COMMAND SECTION************************************  
1400 PRINT "C";
1410 INPUT T$
1412 IF SEG$(T$,1,3) = "NEW" THEN 1622
1420 IF SEG$(T$,1,4) <> "DATA" THEN 1470
1430 D4=1
1440 D$=D$&SEG$(T$,6)&" "
1450 L$="DATA"&D$
1460 GOTO 1400
1470 T1$=SEG$(T$,1,3)
1480 IF D4=0 OR OCC(D$,"*")=1 THEN 1519
1490 PRINT "***BAD DATA - RETYPE FROM ITEM ONE***"
1500 D$=" "
1510 GOTO 1400
1519 REM   
1520 IF T1$ = "FIN" OR T1$ = "STO" THEN 3810
1530 IF T1$ = "PRO" THEN 1010
1540 IF T1$ = "BEG" OR T1$ = "RUN" THEN 1650
1550 IF T1$ = "PUN" THEN 3190
1560 REM 1330 - LIST FOLLOWED BY NUMBER ?  
1570 IF SEG$(T$,1,4) = "LIST" AND LEN(T$)>5 THEN 3030
1575 IF T1$ = "LIS" AND LEN(T$)>4 THEN 3030
1580 IF T1$ = "LIS" THEN 3060
1581 IF T1$ = "LEN" THEN 3561
1590 IF T1$ = "DEL" THEN 3320
1600 IF T1$ = "CHA" THEN 3470
1605 IF T1$ = "ERR" THEN 3281
1610 IF T1$ = "APP" THEN 1120
1620 IF T1$ = "INS" THEN 3360
1621 IF T1$#"NEW" THEN 1630
1622 D5=POS(T$,"DATA")
1623 T$=SEG$(T$,D5)
1624 D$=" "
1625 GOTO 1420
1630 PRINT "***UNRECOGNISABLE COMMAND***"
1640 GOTO 1400
1650 REM ********************EXECUTE SECTION***************************
1655 IF L>1 THEN 1660
1656 GOTO 3080
1657 X=0!REM ZEROIZE ACCUMULATOR 
1660 N=1!REM RESET SCR TO TOP OF PROGRAM STORE   
1661 FOR I=1 TO S1
1662 S$[I]=" "
1663 NEXT I 
1664 REM   
1665 D$=SEG$(L$,5)
1700 IF H=0 PRINT "***HALT FUNCTION OMITTED***"
1710 IF H=0 THEN 1400
1730 REM FIXMEPRINT !PRINT "RESULTS:"
1740 LET Z=1
1750 A$=SEG$(Q$[N],1,3)!REM 1ST THREE LETTERS IN COL.2   
1760 REM SEE IF IT'S MONADIC : 
1770 IF NOT (A$ = "" OR A$ = " ") THEN 1780
1772 PRINT "***ACTION UNSPECIFIED AFTER LINE";N-1;"***"
1774 GOTO 3530
1780 IF A$ = "IN" THEN 2610
1790 IF A$ = "ACC" THEN 2570
1800 IF A$ = "OUT" THEN 2500
1810 IF A$ = "HAL" THEN 3530
1820 IF A$ = "LIN" THEN 2480
1830 IF A$ = "MAP" THEN 2870
1850 LET B$=R$[N]!REM IT'S DYADIC SO GET OTHER PART   
1860 REM *************STORAGE SECTION************************************  
1870 IF A$ <> "STO" THEN 2070
1880 R1$=B$!REM PARAMETER FOR SUB#1
1890 GOSUB 3580
1900 ON I GOTO 3530,1920
1910 REM FIND STORE NAMED IN COL.3 IF IT ALREADY EXISTS:   
1920 FOR P=1 TO Z
1930 IF S$[P] = B$ THEN 2030
1940 NEXT P 
1950 REM   
1960 REM DOES NOT EXIST , SO CREATE IT:
1970 LET S$[Z]=B$
1980 LET S[Z]=X
1990 REM INCREMENT SUBSCRIPT FOR NEXT STORE CREATE/SEARCH  
2000 LET Z=Z+1
2010 GOTO 2040
2020 REM PUT CONTENTS OF ACC. IN REQUIRED STORE
2030 LET S[P]=X
2040 GOTO 3000
2050 REM ********************FUNCTION SUB-SECTION**********************
2060 REM SEARCH FOR FUNCTIONS WITH LABELS OR STRINGS   
2070 IF A$ = "JUM" THEN 2730
2090 IF A$ = "JIN" AND X<0 THEN 2730
2100 IF A$ = "JIZ" AND X=0 THEN 2730
2110 IF A$ = "JIN" OR A$ = "JIZ" THEN 3000
2120 REM B$ MUST NOW BE A CONSTANT OR AN IDENTIFIER , SO SEE IF
2130 REM THE FIRST CHARACTER IS A '+' OR A '-':
2140 IF NOT (SUB$(B$,1) = "+" OR SUB$(B$,1) = "-") GOTO 2200
2150 R2$=B$!REM PARAMETER FOR SUB#2
2160 GOSUB 3680
2170 ON I GOTO 3530,2180
2180 C=VAL(B$)
2190 GOTO 2310
2200 R1$=B$
2210 GOSUB 3580
2220 ON I GOTO 3530,2230
2230 FOR P=1 TO Z!REM STORE SEARCH
2240 IF S$[P] = B$ THEN 2290
2250 NEXT P 
2260 REM   
2270 C=0!REM UNASSIGNED VARIABLE 
2280 GOTO 2300
2290 LET C=S[P]
2300 REM FIND FUNCTION TO BE CARRIED OUT AND PERFORM BASIC EQUIVALENT  
2310 IF A$ = "LOA" THEN 2460
2320 IF A$ = "ADD" THEN 2440
2330 IF A$ = "SUB" THEN 2420
2340 IF A$ = "DIV" THEN 2380
2350 IF A$ = "MUL" THEN 2400
2360 PRINT TAB(1);"***UNRECOGNISABLE INSTRUCTION***"
2370 GOTO 3530
2380 LET X=X/C
2390 GOTO 3000
2400 LET X=X*C
2410 GOTO 3000
2420 LET X=X-C
2430 GOTO 3000
2440 LET X=X+C
2450 GOTO 3000
2460 LET X=C
2470 GOTO 3000
2480 PRINT TAB(1);
2490 GOTO 3000
2500 X$=STR$(X)!PRINT GAP$(9-LEN(X$))&X$;
2510 GOTO 3000
2520 IF CHR(SUB$(B$,1))=18 AND CHR(SUB$(B$,LEN(B$)))=18 THEN 2550
2530 PRINT TAB(1);"***";CHR$(18);"REQUIRED FOR PRINT LITERALS***"
2540 GOTO 3530
2550 PRINT SEG$(B$,2,LEN(B$)-1);
2560 GOTO 3000
2570 PRINT "I";
2580 INPUT X
2590 GOTO 3000
2600 REM ********************DATA-IN SECTION************************** 
2610 REM   
2611 IF D$ = " " THEN 2700
2620 D1=POS(D$," ",1)!D2=POS(D$," ",2)
2630 REM ISOLATE AND EXTRACT DATUM FOROM STRING (D$).  
2640 R2$=SEG$(D$,D1+1,D2-1)
2650 GOSUB 3680
2660 ON I GOTO 3530,2670
2670 X=VAL(R2$)
2680 D$=SEG$(D$,D2)!REM DELETE DATUM JUST READ  
2690 GOTO 2990
2700 PRINT "***PROGRAM REQUIRES MORE DATA***"
2710 GOTO 3530
2720 REM *************JUMP SECTION***************************************  
2730 LET D=N
2740 R1$=R$[D]
2750 GOSUB 3580
2760 ON I GOTO 3530,2770
2770 J=J+1!REM ACOUNT OF JUMPS MADE 
2780 IF J<1000 THEN 2800
2790 PRINT TAB(1);"***TIME LIMIT EXCEEDED***"
2800 FOR N=1 TO L!REM COL.1 LABEL SEARCH  
2810 IF P$[N] = R$[D] THEN 1750
2820 NEXT N 
2830 REM   
2840 N=D!REM FOR ERROR MESSAGE  
2850 PRINT TAB(1);"***UNDEFINED LABEL***"
2860 GOTO 3530
2865 REM *********************'MAP' INSTRUCTION**************************  
2870 PRINT TAB(1);"MAP INSTRUCTION IN LINE";N
2871 PRINT "STATE OF STORE:"
2872 PRINT "ACCUMULATOR",X
2874 FOR M=1 TO S1
2875 IF S$[M] = " " THEN 2878
2876 PRINT S$[M],S[M]
2877 NEXT M 
2878 REM   
2879 PRINT "DATA UNUSED: ";D$
2880 PRINT 
2890 GOTO 3000
2980 REM ***************************************************************   
2990 REM INCREMENT S.C.R. AND GO TO GET NEXT LINE FROM STORE.  
3000 LET N=N+1
3010 GOTO 1750
3020 REM ********************LISTING SECTION****************************   
3030 L1=POS(T$," ")
3040 L1=L2=VAL(SEG$(T$,L1+1))!REM LIST THAT LINE   
3050 GOTO 3070
3060 L1=1!L2=L!REM LIST WHOLE PROGRAM   
3070 IF L>0 THEN 3100
3080 PRINT "***NO PROGRAM***"
3090 GOTO 1400
3100 PRINT 
3110 FOR F=L1 TO L2
3120 IF Q$[F] = " " THEN 3140
3130 PRINT P$[F];TAB(10);Q$[F];TAB(20);R$[F]
3140 NEXT F 
3150 REM   
3160 IF L1=L2 THEN 1400
3161 PRINT "%"
3162 GOSUB 3792
3163 GOTO 1400
3165 PRINT 
3170 GOTO 1400
3180 REM ********************PUNCH SECTION******************************   
3190 IF L=0 THEN 3080
3200 PRINT MUL$(" ",30)
3210 FOR F=1 TO S2
3220 PRINT P$[F];" ";Q$[F];" ";R$[F]
3225 IF P$[F] = "%" THEN 3240
3230 NEXT F 
3240 REM   
3250 GOSUB 3792
3260 PRINT MUL$(" ",30)
3270 GOTO 1400
3280 REM **************EDITING SECTION***********************************  
3281 GOSUB 3671!IF I=1 THEN 1400!E=VAL(R$)
3282 FOR I=L-E TO L-1
3283 P$[I]=Q$[I]=R$[I]=" "
3284 NEXT I 
3285 GOTO 1400
3290 E=VAL(R$[N])
3300 N=N-E-1!REM DELETE LAST E LINES   
3310 GOTO 1330
3320 GOSUB 3671!IF I=1 THEN 1400
3330 E=VAL(R$)
3340 P$[E]=Q$[E]=R$[E]=" "!REM DELETE LINE  
3342 IF E >= L THEN 1400
3343 FOR E1=E TO L-1
3344 P$[E1]=P$[E1+1]
3345 Q$[E1]=Q$[E1+1]
3346 R$[E1]=R$[E1+1]
3347 NEXT E1 
3348 REM   
3349 L=L-1
3350 GOTO 1400
3360 REM 'INSERT'  
3370 GOSUB 3671!IF I=1 THEN 1400
3380 S=VAL(R$)
3382 IF Q$[S+1] = " " OR Q$[S+1] = "" THEN 3440
3390 FOR I=L TO S+1 STEP -1
3400 P$[I+1]=P$[I]
3410 Q$[I+1]=Q$[I]
3420 R$[I+1]=R$[I]
3430 NEXT I 
3435 REM   
3436 L=L+1!REM LINE WILL BE ADDED   
3440 N=S+1
3450 GOSUB 1150
3460 ON I GOTO 1400,3290,1400
3470 GOSUB 3671!IF I=1 THEN 1400
3480 N=VAL(R$)
3481 IF Q$[N]#"" THEN 3485
3482 PRINT "***LINE";N;"DOES NOT EXIST***"
3483 GOTO 1400
3485 IF Q$[N] = "HAL" LET H=H-1
3486 IF Q$[N] = " " OR Q$[N] = "" THEN 3482
3490 PRINT "TO ";
3500 GOSUB 1150
3510 ON I GOTO 1400,3290,1400
3520 GOTO 1400
3525 REM ********************END OF PROGRAM MESS.************************  
3530 REM FIXMEPRINT !PRINT "LINE";N;"STOP"!PRINT 
3540 D3=OCC(D$," ")+OCC(D$,"*")
3550 IF LEN(D$)-D3>0 THEN  PRINT "***DATA REMAINING UNUSED***"
3560 GOTO 1400
3561 PRINT "PROGRAM SIZE:";ABS(L-1);"INS . MAX IS";S2;"."
3563 GOTO 1400
3570 REM ******************SUBROUTINES**********************************   
3580 REM SUBROUTINE#1 IDENTIFIER/LABEL VALIDATION PARA R1$ LOCAL R 
3590 IF LEN(R1$)>6 OR CHR(R1$)>58 THEN 3660
3600 FOR I=2 TO LEN(R1$)
3610 R=CHR(SUB$(R1$,I))
3620 IF (R>9 AND R<33) OR (R>58) THEN 3660
3630 NEXT I 
3640 REM   
3650 I=2!GOTO 3670
3660 I=1!PRINT TAB(1);"***INVALID IDENTIFIER/LABEL***"
3670 RETURN
3671 R$=SEG$(T$,POS(T$," ")+1)
3672 FOR I=1 TO LEN(R$)
3673 IF R$ = " " OR R$ = "" THEN 3677
3674 IF CHR(SUB$(R$,I))>9 THEN 3677
3675 NEXT I 
3676 I=2!GOTO 3679
3677 PRINT "***OPERATOR MISSING OR INVALID***"
3678 I=1
3679 RETURN
3680 REM PSUBROUTINE#2 DATA/CONSTANT VALIDATION PARA R2$ LOCAL R   
3688 REM SUBROUTINE #3 
3690 R=CHR(R2$)
3700 IF LEN(R2$)>7 OR NOT (R<10 OR R=27 OR R=29 OR R=30) THEN 3760
3710 FOR I=2 TO LEN(R2$)
3720 IF CHR(SUB$(R2$,I))>9 AND NOT (SUB$(R2$,I) = ".") THEN 3760
3730 NEXT I 
3740 REM   
3750 I=2!GOTO 3791
3760 IF R2$ = "*" THEN 3790
3770 I=1!PRINT TAB(1);"***INVALID DATA/CONSTANT***"
3780 GOTO 3791
3790 I=1!PRINT TAB(1);"***PROGRAM REQUIRES MORE DATA***"
3791 RETURN
3792 REM DATA PRINTING SUBROUTINE  
3793 D2$=D$!D$=L$
3794 IF LEN(D$)>66 THEN 3797
3795 PRINT "DATA";DEL$(D$,"DATA")
3796 GOTO 3801
3797 S3=OCC(SEG$(D$,1,66)," ")
3798 PRINT SEG$(D$,1,POS(D$," ",S3-1))
3799 D$=SEG$(D$,POS(D$," ",S3-1))
3800 GOTO 3794
3801 D$=D2$
3802 RETURN
3810 T3$=TIM$
3811 T=VAL(SEG$(T2$,1,2))*3600+VAL(SEG$(T2$,4,5))*60+VAL(SEG$(T2$,7,8))
3812 T=VAL(SEG$(T3$,1,2))*3600+VAL(SEG$(T3$,4,5))*60+VAL(SEG$(T3$,7,8))-T
3813 T=INT(T/60+.5)
3814 PRINT "END OF CESIL. TIME USED";T;"MINUTES."
4000 END