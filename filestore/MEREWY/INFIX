BU
10 PRINT
20 REM (** INFIX/POSTFIX CONVERSION D.BONHAM 18/1/77 **)
30 PRINT "DO YOU REQUIRE INSTRUCTIONS";
40 INPUT I$
50 IF I$="YES" THEN 100
60 IF I$="NO" THEN 160
70 PRINT "YES OR NO";
80 GOTO 40
90 REM (** PROGRAM DETAILS ARE PRINTED OUT **)
100 PRINT "THIS PROGRAM CONVERTS AN INFI EXPRESSION TO ITS POSTFIX EQUIVALENT"
110 PRINT "POSTFIX FORMAT. THE INPUT EXPRESSION MUST CONSIST OF:"
120 PRINT "A)AT LEAST ONE OF THE OPERATORS +,-,*,/,^."
130 PRINT "B)AT LEAST TWO SINGLE LETTER VARIABLES (A TO Z)."
140 PRINT "NO SPACES ARE ALLOWED"
150 REM (** SET <CHAR.SCANNED>,<SUBR.VAR>,<POST.EXPRESS>,<STACK> TO 0 **)
160 C$=O$=P$=S$=""
170 REM (** INPUT INFIX EXPRESSION INTO I$ **)
180 PRINT "WHAT IS INFIX EXPRESSION";
190 INPUT I$
200 REM (** EXAMINE STRING ONE CHARACTER AT A TIME **)
210 FOR I=1 TO LEN(I$)
220 REM (** TAKE 'I'TH CHARACTER **)
230 C$ = SUB$(I$,I)
240 IF NOT (C$ >= "A" AND C$ <= "Z") THEN 290
250 REM (** APPEND VARIABLE TO POSTFIX EXPRESSION **)
260 P$=P$&C$
270 GOTO 500
280 REM (** TEST FOR VALID OPERATOR **)
290 IF OCC("+-*/^", C$)=1 THEN 330
300 REM (** ERROR MESSAGE **)
310 PRINT I$;TAB(I);"^ -- INVALID CHARACTER"
320 GOTO 570
330 O$=C$
340 REM (** FIND PRECEDENCE LEVEL OF OPERATOR AND COPY TO P **)
350 GOSUB 640
360 C=P
370 REM (** REPEAT FOR CHARACTER AT END OF STACK **)
380 O$=SUB$(S$,LEN(S$))
390 GOSUB 640
400 REM (** IF OP AT END LOWER THAN NEW, TRANSFER TO POSTFIX **)
410 IF P<C THEN 490
420 P$=P$&O$
430 REM (** DELETE TRANSFERRED CHARACTER **)
440 S$=SEG$(S$,1,LEN(S$)-1)
450 REM (** TEST FOR EMPTY STACK **)
460 IF S$ = "" THEN 490
470 GOTO 380
480 REM (** ADD CHARACTER TO END OF STACK **)
490 S$=S$&C$
500 NEXT I
510 IF S$ = "" THEN 560
520 REM (** TRANSFER CHARACTERS FROM END OF STACK TO POSTFIX **)
530 FOR I=LEN(S$) TO 1 STEP -1
540 P$=P$&SUB$(S$,I)
550 NEXT I
560 PRINT "POSTFIX EXPRESSION:";P$!PRINT
570 PRINT "DO YOU WISH TO INPUT ANOTHER INFIX EXPRESSION";
580 INPUT I$
590 IF I$ = "YES" THEN 160
600 IF I$ = "NO" STOP
610 PRINT "YES OR NO";
620 GOTO 580
630 REM (** SUBROUTINE 1 **)
640 IF OCC("+-", O$)=1 LET P=1
650 IF OCC("*/", O$)=1 LET P=2
660 IF O$="^" LET P=3
670 IF O$="" LET P=0
680 RETURN
690 END